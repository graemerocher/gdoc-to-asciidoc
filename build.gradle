plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin:"groovy"
apply plugin:"maven-publish"

repositories {
	maven {
		url "https://repo.grails.org/grails/core"
	}
}

group "org.grails"
version "1.0.0"

dependencies {
	compile gradleApi()
	compile "org.codehaus.groovy:groovy:2.4.6"
	compile "org.grails:grails-docs:3.1.8"
	compile "org.yaml:snakeyaml:1.16"

	testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
		exclude module:"groovy-all"
	}
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type:Jar) {
    classifier = 'javadoc'
    from groovydoc.outputs
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : ''
    key = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : ''
    publications = ['maven']
    publish = true
    pkg {
        repo = "grails-core"
        userOrg = project.hasProperty('userOrg') ? project.userOrg : 'grails'
        name = "gdoc-to-asciidoc"
        desc = "Converts Grails' GDoc format to AsciiDoc"
        websiteUrl = "https://github.com/graemerocher/gdoc-to-asciidoc"
        issueTrackerUrl = "https://github.com/graemerocher/gdoc-to-asciidoc/issues"
        vcsUrl = "git@github.com:graemerocher/gdoc-to-asciidoc.git"
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}